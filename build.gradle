// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'versions.gradle'
    apply from: 'dependencies-functions.gradle'
    apply from: 'android.gradle'

    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        //noinspection JcenterRepositoryObsolete
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$versions.kotlinVersion"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$versions.daggerVersion"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {

    if (project.name == excludeParentFoldersFor(project)) {
        return
    }

    if (project.name == "app") {
        apply plugin: 'com.android.application'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: '../build-config.gradle'
        applyAndroid(it)
        return
    }

    if (project.name.contains("domain")) {
        apply plugin: 'kotlin'
        apply plugin: 'kotlin-kapt'
        return
    }

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "${rootProject.projectDir}/build-config.gradle"
    applyAndroid(it)
}

static def excludeParentFoldersFor(Project project) {
    def name = ""
    def allProjects = project.getAllprojects()
    if (allProjects.size() > 1) {
        name = allProjects.first().name
    }
    return name
}